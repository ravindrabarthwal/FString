apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

version '1.0.1' // your library version
group 'com.ravindrabarthwal.fstring' // whatever group name you'd like

publishing {
    publications {
        Production(MavenPublication) {
            artifact("$buildDir/outputs/aar/fstring-release.aar") // replace with your library name
            groupId this.group
            artifactId 'fstring' // replace with your library artifact id
            version this.version

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                // Iterate over the implementation dependencies (we don't want the test ones),
                // adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included in the pom.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives sourcesJar
}

bintray {
    user = System.getenv('BINTRAY_ID')
    key = System.getenv('BINTRAY_API_KEY')
    publications = ['Production']
    configurations = ['archives']
    // Default: false. Whether to run this as dry-run, without deploying
    dryRun = false
    // Default: false. Whether to override version artifacts already published
    override = false
    // Default: false. Whether version should be auto published after an upload
    publish = true
    pkg {
        repo = 'com.ravindrabarthwal.fstring'
        name = 'fstring'
        description = "Dynamically updatable strings.xml wrapper for Android"
        publicDownloadNumbers = true
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/ravindrabarthwal/FString.git'
        userOrg = "barthwal"
        version {
            name = this.version
            desc = "Version ${this.version}"
            released = new Date()
            vcsTag = this.version
        }
    }
}